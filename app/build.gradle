apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    //noinspection GroovyAssignabilityCheck
    if(project.hasProperty("keystore.properties")
            && new File(project.property("keystore.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("keystore.properties"))))

        signingConfigs {
            release {
                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    } else {
        //noinspection GroovyMissingReturnStatement
        signingConfigs {
            release {
            }
        }
    }

    defaultConfig {
        applicationId "com.abdodaoud.merlin"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 2
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:customtabs:$support_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.anko:anko-sdk15:$anko_version"
    compile "org.jetbrains.anko:anko-support-v4:$anko_version"
    compile "com.google.code.gson:gson:2.4"
    compile 'com.wdullaer:materialdatetimepicker:2.0.2'
    compile 'com.anjlab.android.iab.v3:library:1.0.28@aar'
    compile 'me.mvdw.recyclerviewmergeadapter:recyclerviewmergeadapter:1.0.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    compile('com.mikepenz:materialdrawer:4.5.8@aar') {
        transitive = true
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}